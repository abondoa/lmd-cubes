prefix bif: <http://example.org/customfunction/>
prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>
prefix qb:   <http://purl.org/linked-data/cube#>
prefix qb4o: <http://publishing-multidimensional-data.googlecode.com/git/index.html#ref_qbplus_>

select
  ?cntrycode
  ((count (1)) as ?numcust)
  (sum (?c_acctbal) as ?totacctbal)
where 
{
  ?cust ltpch:customer_acctbal ?c_acctbal ;
        ltpch:customer_phone ?c_phone .
  {
    select 
      ((avg (?c_acctbal2)) as ?acctbal_threshold)
    where
    {
      ?cust2 ltpch:customer_acctbal ?c_acctbal2 ;
             ltpch:customer_phone ?c_phone2 .
      FILTER ((?c_acctbal2 > 0.00) &&
        SUBSTR(?c_phone2, 1, 2) in (%COUNTRY_CODE_SET%) )
    } 
  }
  FILTER (SUBSTR(?c_phone, 1, 2) in (%COUNTRY_CODE_SET%) && ?c_acctbal > ?acctbal_threshold) .
  FILTER not exists { ?ord ltpch:o_has_customer ?cust } .
}
group by ((SUBSTR(?c_phone, 1, 2)) as ?cntrycode)
order by (SUBSTR(?c_phone, 1, 2))
