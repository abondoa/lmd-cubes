prefix bif: <http://example.org/customfunction/>
prefix qb:   <http://purl.org/linked-data/cube#>
prefix qb4o: <http://publishing-multidimensional-data.googlecode.com/git/index.html#ref_qbplus_>
prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>

select
  ?bigpspart
  ?bigpsvalue
where 
{
      { 
        select
          ?bigpspart
          (sum(?b_supplycost * ?b_availqty) as ?bigpsvalue)
        where
        {
          ?bigps qb:dataSet ltpch:partSupplierCube ;
                 ltpch:ps_has_part ?bigpspart ;
                 ltpch:ps_supplycost ?b_supplycost ;
                 ltpch:ps_availqty ?b_availqty ;
                 ltpch:ps_has_supplier ?b_supplier .
          ?b_supplier ltpch:nation_name ?n_name .
          FILTER(?n_name = "%NATION%") .
        }
        group by 
          ?bigpspart
      } .
      {
        select
          ((sum(?t_supplycost * ?t_availqty) * %FRACTION%) as ?threshold)
        where
        {
          ?thr_ps qb:dataSet ltpch:partSupplierCube ;
                  ltpch:ps_has_part ?t_part ;
                  ltpch:ps_supplycost ?t_supplycost ;
                  ltpch:ps_availqty ?t_availqty ;
                  ltpch:ps_has_supplier ?t_supplier .
          ?t_supplier ltpch:nation_name ?n_name .
          FILTER(?n_name = "%NATION%") .
        }
      }
    filter (?bigpsvalue > ?threshold)
  }
order by
  desc (?bigpsvalue)

