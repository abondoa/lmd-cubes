prefix bif: <http://example.org/customfunction/>
prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>
prefix qb:   <http://purl.org/linked-data/cube#>
prefix qb4o: <http://publishing-multidimensional-data.googlecode.com/git/index.html#ref_qbplus_>

select
  ?o_orderkey
  (sum(?l_lineextendedprice*(1 - ?l_linediscount)) as ?revenue)
  ?o_orderdate
  ?o_shippriority
where  {
  ?li qb:dataSet ltpch:lineitemCube ;
	  ltpch:l_lineextendedprice ?l_lineextendedprice ;
    ltpch:l_linediscount ?l_linediscount ;
    ltpch:l_shipdate ?l_shipdate ;
    ltpch:order_order_orderdate ?o_orderdate ;
    ltpch:order_order_shippriority ?o_shippriority ;
    ltpch:order_order_orderkey ?o_orderkey ;
    ltpch:order_customer_mktsegment ?c_mktsegment .
  filter ((?o_orderdate < "%DATE%"^^xsd:date) &&
    (?l_shipdate > "%DATE%"^^xsd:date) &&
    (?c_mktsegment = "%SEGMENT%") )  
}
group by
  ?o_orderkey
  ?o_orderdate
  ?o_shippriority
order by
  desc (sum (?l_lineextendedprice * (1 - ?l_linediscount)))
  ?o_orderdate
limit 10
