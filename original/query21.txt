prefix bif: <http://example.org/customfunction/>
prefix ltpch: <http://lod2.eu/schemas/rdfh#>

select
    ?s_name,
    (count(1)) as ?numwait
where {
         ?supp a ltpch:supplier ;
               ltpch:s_name ?s_name ;
               ltpch:s_has_nation ?s_nation .
         ?s_nation ltpch:n_name "%NATION%" .
         ?li1 a ltpch:lineitem ;
              ltpch:l_receiptdate ?l1_receiptdate ;
              ltpch:l_commitdate ?l1_commitdate ;
              ltpch:l_has_supplier ?supp ;
              ltpch:l_has_order ?ord .
         ?ord ltpch:o_orderstatus "F" .
         filter ( ?l1_receiptdate > ?l1_commitdate)
         filter exists {
              ?li2 ltpch:l_has_order ?ord ;
                   ltpch:l_has_supplier ?supp2 .
              filter (?supp != ?supp2)
         }
         filter not exists {
              ?li3 ltpch:l_has_order ?ord ;
                   ltpch:l_receiptdate ?l3_receiptdate ;
                   ltpch:l_commitdate ?l3_commitdate ;
                   ltpch:l_has_supplier ?supp3 .
              filter (
                 ?l3_receiptdate > ?l3_commitdate &&
                 ?supp3 != ?supp
              )
         }
       }
group by
   ?s_name
order by
    desc (count(1))
    ?s_name
limit 100
