prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?o_orderkey
  (sum(?l_lineextendedprice*(1 - ?l_linediscount)) as ?revenue)
  ?o_orderdate
  ?o_shippriority
where  {
  ?li qb:dataSet ltpch:lineitemCube ;
	  ltpch:l_lineextendedprice ?l_lineextendedprice ;
    ltpch:l_linediscount ?l_linediscount ;
    ltpch:l_has_order ?ord ;
    ltpch:l_shipdate ?l_shipdate .
  ?ord ltpch:o_orderdate ?o_orderdate ;
    ltpch:o_shippriority ?o_shippriority ;
    ltpch:o_orderkey ?o_orderkey ;
    ltpch:o_has_customer ?cust .
  ?cust ltpch:c_mktsegment ?c_mktsegment .
  filter ((?o_orderdate < "%DATE%"^^xsd:date) &&
    (?l_shipdate > "%DATE%"^^xsd:date) &&
    (?c_mktsegment = "%SEGMENT%") ) 
}
group by
  ?o_orderkey
  ?o_orderdate
  ?o_shippriority
order by
  desc (sum (?l_lineextendedprice * (1 - ?l_linediscount)))
  ?o_orderdate
limit 10
